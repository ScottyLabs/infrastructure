# Enables machine authentication for NixOS hosts
resource "vault_auth_backend" "approle" {
  type = "approle"
  path = "approle"
}

# Mapping of hosts to projects they run, generated by Nix
locals {
  host_projects = jsondecode(file("${path.module}/host-projects.json"))
}

# Each host gets a policy granting read access to its projects' prod secrets
resource "vault_policy" "host" {
  for_each = local.host_projects
  name     = "host-${each.key}"
  policy   = length(each.value) > 0 ? join("\n", [
    for project in each.value : <<-EOT
      path "secret/data/projects/${project}/prod/*" {
        capabilities = ["read"]
      }
      path "secret/metadata/projects/${project}/prod/*" {
        capabilities = ["list", "read"]
      }
    EOT
  ]) : "# No project access"
}

# AppRole for each host
resource "vault_approle_auth_backend_role" "host" {
  for_each       = local.host_projects
  backend        = vault_auth_backend.approle.path
  role_name      = each.key
  token_policies = [vault_policy.host[each.key].name, vault_policy.infra.name]

  token_ttl     = 3600   # 1 hour
  token_max_ttl = 86400  # 24 hours
  secret_id_ttl = 0      # no expiration
}

output "approle_role_ids" {
  value = { for k, v in vault_approle_auth_backend_role.host : k => v.role_id }
}
